locals {
  config_patches = [for f in sort(fileset("${path.root}/../talos/patches", "*.yaml")) :
    file("${path.root}/../talos/patches/${f}")]

  vip_patch = yamlencode({
    machine = {
      network = {
        interfaces = [{
          interface = var.talos.vip_interface
          vip = {
            ip = var.talos.vip
          }
        }]
      }
    }
  })

  controlplane_patches = concat(local.config_patches, [local.vip_patch])
}

resource "talos_machine_secrets" "machine_secrets" {}

data "talos_client_configuration" "talosconfig" {
  cluster_name         = var.talos.cluster_name
  client_configuration = talos_machine_secrets.machine_secrets.client_configuration
  endpoints            = [var.talos.vip]
}

data "talos_machine_configuration" "machineconfig_cp" {
  for_each         = { for ip in var.control_plane_ips : ip => ip }
  cluster_name     = var.talos.cluster_name
  cluster_endpoint = "https://${var.talos.vip}:6443"
  machine_type     = "controlplane"
  machine_secrets  = talos_machine_secrets.machine_secrets.machine_secrets
  config_patches   = local.controlplane_patches
}

resource "talos_machine_configuration_apply" "cp_config_apply" {
  for_each                    = { for ip in var.control_plane_ips : ip => ip }
  depends_on                  = [var.talos_depends_on]
  client_configuration        = talos_machine_secrets.machine_secrets.client_configuration
  machine_configuration_input = data.talos_machine_configuration.machineconfig_cp[each.key].machine_configuration
  node                        = each.key
}

data "talos_machine_configuration" "machineconfig_worker" {
  for_each         = { for ip in var.worker_ips : ip => ip }
  cluster_name     = var.talos.cluster_name
  cluster_endpoint = "https://${var.talos.vip}:6443"
  machine_type     = "worker"
  machine_secrets  = talos_machine_secrets.machine_secrets.machine_secrets
  config_patches   = local.config_patches
}

resource "talos_machine_configuration_apply" "worker_config_apply" {
  for_each                    = { for ip in var.worker_ips : ip => ip }
  depends_on                  = [var.talos_depends_on]
  client_configuration        = talos_machine_secrets.machine_secrets.client_configuration
  machine_configuration_input = data.talos_machine_configuration.machineconfig_worker[each.key].machine_configuration
  node                        = each.key
}

resource "talos_machine_bootstrap" "bootstrap" {
  depends_on           = [talos_machine_configuration_apply.cp_config_apply]
  client_configuration = talos_machine_secrets.machine_secrets.client_configuration
  node                 = var.control_plane_ips[0]
}

data "talos_cluster_health" "health" {
  depends_on           = [talos_machine_bootstrap.bootstrap, talos_machine_configuration_apply.cp_config_apply, talos_machine_configuration_apply.worker_config_apply]
  client_configuration = data.talos_client_configuration.talosconfig.client_configuration
  control_plane_nodes  = var.control_plane_ips
  worker_nodes         = var.worker_ips
  endpoints            = data.talos_client_configuration.talosconfig.endpoints
}

resource "talos_cluster_kubeconfig" "kubeconfig" {
  depends_on           = [talos_machine_bootstrap.bootstrap, data.talos_cluster_health.health]
  client_configuration = talos_machine_secrets.machine_secrets.client_configuration
  node                 = var.control_plane_ips[0]
}
